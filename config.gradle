// 全局配置

ext {

    targetSdkVersion = 27
    compileSdkVersion = 27
    minSdkVersion = 16
    buildToolsVersion = '27.0.3'

    versionCode = 1
    versionName = '1.0.0'

    libs = [
            'junit'            : 'junit:junit:4.12',
            'runner'           : 'com.android.support.test:runner:1.0.1',
            'espresso'         : 'com.android.support.test.espresso:espresso-core:3.0.1',
            'support-v7'       : 'com.android.support:appcompat-v7:27.0.2',
            'design'           : 'com.android.support:design:27.0.2',
            'constraint-layout': 'com.android.support.constraint:constraint-layout:1.0.2',
            'kotlin'           : 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.2.41',
            'atlas-core'       : "com.taobao.android:atlas_core:5.1.0.0@aar",
            'atlas-update'     : "com.taobao.android:atlasupdate:1.1.4.21@aar",

            'NLogger'          : 'com.github.hijesse:android-logger:2.5.0',
            'Toasty'           : 'com.github.GrenderG:Toasty:1.3.0',
            'ZXing'            : 'com.journeyapps:zxing-android-embedded:3.6.0',
    ]

    atlasConfig = [
            'autoStartBundles': 'cn.jesse.gaea.plugin.main,cn.jesse.gaea.plugin.user',
            'outOfApkBundles' : computeRemoteBundles()
    ]
}

/**
 * 提供计算远程bundle的配置, 方便在dev时开发调试
 *
 * @return 远程bundle
 */
def computeRemoteBundles() {
    String remoteBundles = ""

    final boolean isScannerRemote = false

    if (isScannerRemote) {
        remoteBundles = addSplit(remoteBundles, 'remote.scanner')
    }

    print("computeRemoteBundles: ${remoteBundles} \n")
    return remoteBundles
}

/**
 * 根据情况判断是否添加分隔符
 *
 * @param origin 原bundle
 * @param bundle 要添加的bundle
 * @return bundle 或者添加分隔符
 */
def addSplit(String origin, String bundle) {
    if (origin.empty) {
        return bundle
    }
    return "${origin},${bundle}"
}