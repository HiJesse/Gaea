// 全局配置

ext {

    applicationId = 'cn.jesse.gaea'
    targetSdkVersion = 27
    compileSdkVersion = 27
    minSdkVersion = 16
    buildToolsVersion = '27.0.3'

    versionCode = 1
    versionName = '1.0.1'

    // 基准包版本
    apVersion = getConfigValue('apVersion', null)

    libs = [
            'junit'               : 'junit:junit:4.12',
            'runner'              : 'com.android.support.test:runner:1.0.1',
            'espresso'            : 'com.android.support.test.espresso:espresso-core:3.0.1',
            'support-v7'          : 'com.android.support:appcompat-v7:27.0.2',
            'design'              : 'com.android.support:design:27.0.2',
            'constraint-layout'   : 'com.android.support.constraint:constraint-layout:1.0.2',
            'kotlin'              : 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.2.41',
            'atlas-core'          : 'com.taobao.android:atlas_core:5.1.0.0@aar',
            'atlas-update'        : 'com.taobao.android:atlasupdate:1.1.4.21@aar',
            'lifecycle-runtime'   : 'android.arch.lifecycle:runtime:1.1.1',
            'lifecycle-extensions': 'android.arch.lifecycle:extensions:1.1.1',
            'lifecycle-compiler'  : 'android.arch.lifecycle:compiler:1.1.1',

            'retrofit'            : 'com.squareup.retrofit2:retrofit:2.4.0',
            'retrofit-gson'       : 'com.squareup.retrofit2:converter-gson:2.4.0',
            'retrofit-rxjava'     : 'com.squareup.retrofit2:adapter-rxjava2:2.4.0',
            'http-log'            : 'com.squareup.okhttp3:logging-interceptor:3.10.0',
            'rxjava'              : 'io.reactivex.rxjava2:rxjava:2.1.14',
            'rxandroid'           : 'io.reactivex.rxjava2:rxandroid:2.0.2',
            'NLogger'             : 'com.github.hijesse:android-logger:2.5.0',
            'Toasty'              : 'com.github.GrenderG:Toasty:1.3.0',
            'ZXing'               : 'com.journeyapps:zxing-android-embedded:3.6.0',
            'dialog'              : 'com.kongzue.dialog:dialog:2.2.3',
            'file-downloader'     : 'com.liulishuo.filedownloader:library:1.7.4',
    ]

    atlasConfig = [
            'autoStartBundles': 'cn.jesse.gaea.plugin.main,cn.jesse.gaea.plugin.user',
            'outOfApkBundles' : computeRemoteBundles()
    ]
}

/**
 * 提供计算远程bundle的配置, 方便在dev时开发调试
 *
 * @return 远程bundle
 */
def computeRemoteBundles() {
    String remoteBundles = ""

    final boolean isScannerRemote = true

    if (isScannerRemote) {
        remoteBundles = addSplit(remoteBundles, 'remote.scanner')
    }

    print("computeRemoteBundles: ${remoteBundles} \n")
    return remoteBundles
}

/**
 * 根据情况判断是否添加分隔符
 *
 * @param origin 原bundle
 * @param bundle 要添加的bundle
 * @return bundle 或者添加分隔符
 */
def addSplit(String origin, String bundle) {
    if (origin.empty) {
        return bundle
    }
    return "${origin},${bundle}"
}

/**
 * 获取命令行参数
 *
 * @param key 键
 * @param defValue 默认值
 * @return
 */
def getConfigValue(key, defValue) {
    def val = System.getProperty(key)
    if (null != val) {
        return val
    }
    val = System.getenv(key)
    if (null != val) {
        return val
    }
    return defValue
}